buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def resolveReactNativeDirectory() {
    def reactNativeFromNodeModules = file("${projectDir}/../../react-native")
    if (reactNativeFromNodeModules.exists()) {
        return reactNativeFromNodeModules
    }
    def reactNativeFromMonorepo = file("${projectDir}/../../../node_modules/react-native")
    if (reactNativeFromMonorepo.exists()) {
        return reactNativeFromMonorepo
    }
}


def reactNativeRootDir = resolveReactNativeDirectory().path


android {
    // See https://github.com/facebookincubator/fbjni/issues/50
    buildFeatures {
        prefab true //important for JSI
    }
    dependencies {
        implementation 'com.facebook.fbjni:fbjni:0.3.0' //important for JSI
    }
    configurations {
       all*.exclude module: 'fbjni-java-only' //important for JSI
    }

    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                arguments "-DANDROID_STL=c++_shared" //important for JSI
            }
        }
    }

    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Debug", "-DREACT_NATIVE_DIR=${reactNativeRootDir}"
                }
            }
        }

        release {
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=RelWithDebInfo", "-DREACT_NATIVE_DIR=${reactNativeRootDir}"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        doNotStrip '**/*.so'
        excludes = [
                "**/libc++_shared.so",
                "**/libfbjni.so",
                "**/libjsi.so",
                "**/libfolly_json.so",
                "**/libglog.so",
                "**/libhermes.so",
                "**/libreactnativejni.so",
                "**/libjscexecutor.so",
                "**/libturbomodulejsijni.so"
        ]
    }
}

repositories {
    mavenLocal()
    maven {
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.facebook.react:react-native:+"
}

android.defaultConfig.ndk.debugSymbolLevel = 'FULL'
